
plugins {
	id 'com.stehno.natives' version '0.2.4'
}

apply plugin: 'java'
apply plugin: 'application'

mainClassName='org.subaquatics.javarov.Main'

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

def jInputVersion = '2.0.6'

project.ext.set( 'nativeLibsDir', "$buildDir/libs/natives" )

dependencies {
	compile group: 'de.root1', name:'rxtx-rebundled', version:'2.1-7r2-6'
	compile group: 'net.java.jinput', name:'jinput-platform', version:'2.0.6'
	compile group: 'net.java.jinput', name:'jinput', version:'2.0.6'
	compile 'com.github.SINTEF-9012:rtcharts:bd26db7'
	compile 'com.miglayout:miglayout-swing:4.2'
	runtime files("$buildDir/natives/windows/")
}

natives {
	jars = [
		"jinput-platform-2.0.6-natives-windows.jar",
		"jinput-platform-2.0.6-natives-osx.jar",
		"jinput-platform-2.0.6-natives-linux.jar"
	]
	platforms = 'windows'
}

run {
	systemProperty 'java.library.path', 'build/natives/windows'
}

startScripts {
	def jvmOpts = "-Djava.library.path=lib"
	inputs.property("jvmOpts", {jvmOpts})
	doLast {
		def optsEnvVar = "DEFAULT_JVM_OPTS"
		unixScript.text = unixScript.text.replace("$optsEnvVar=${'""'}", "$optsEnvVar=${'"'}$jvmOpts${'"'}")
		windowsScript.text = windowsScript.text.replace("set $optsEnvVar=", "set $optsEnvVar=$jvmOpts")
	}
}